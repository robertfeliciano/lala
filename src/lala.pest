program = _{ SOI ~ "\n"* ~ (stmt ~ "\n"+)* ~ stmt? ~ EOI }

stmt = _{ expr } 

expr = {
    assn
  | monadic
  | dyadic
  | ident
  | matrix
  | terms
  | command
}

monadic = { verb ~ expr }

dyadic = { (monadic | terms | ident ) ~ verb ~ expr }

assn = { "let" ~ ident ~ "=" ~ expr }

matrix = {
    (terms+ ~ ";")+ ~ terms ~ ";"?
}

terms = { term+ }

term = _{
    decimal
  | integer
  | "(" ~ expr ~ ")"
}

verb = {
    "#" // rank

  | "?" // inverse

  | "rref" // rref

  | "%" // transpose

  | "det" // determinant

  | "@" // dot prod

  | "++" // matrix addition

  | "**" // elem wise multiplication

  | "!" // scalar multiplication
  // more to come later
}

integer = @{ "-"? ~ "_"? ~ ASCII_DIGIT+ }

decimal = @{
    ("_"? ~ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT*)
  | ("-"? ~ ASCII_DIGIT ~ "e" ~ "-"? ~ ASCII_DIGIT)
}

ident = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }

cmd = @{
	ASCII_ALPHANUMERIC+
}

cmd_param = @{ (!WHITESPACE~ ANY)+ }
cmd_params = { cmd_param+ }

command = { "/" ~ cmd ~ cmd_params* }

WHITESPACE = _{ " " | "\t" }

COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" | "//" ~ (!"\n" ~ ANY)* }
